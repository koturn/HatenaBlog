VimでJsonを整形したい
=====================

[jq](http://stedolan.github.io/jq/)という非常に便利なコマンドラインツールがある．
これは，jsonの整形を行うものだ．
jqコマンド自体については，[公式のマニュアル](http://stedolan.github.io/jq/manual/)や以下の記事を参考にすればよくわかるだろう．

- [jq コマンドを使う日常のご紹介 - Qiita](http://qiita.com/takeshinoda@github/items/2dec7a72930ec1f658af)
- [軽量JSONパーサー『jq』のドキュメント：『jq Manual』をざっくり日本語訳してみました ｜ Developers.IO](http://dev.classmethod.jp/tool/jq-manual-japanese-translation-roughly/)
- [jq コマンドが強力すぎてヤバい件 | CUBE SUGAR STORAGE](http://momijiame.tumblr.com/post/51299899637/jq)
- [jqコマンドが実は高性能すぎてビビッた話 - beatsync.net](https://beatsync.net/main/log20130428.html)

この便利なjqコマンドをVim上で利用したい．
そのように考えた人は既におり，以下のいくつかの記事が参考になる．

- [Vim上でjqでjsonを整形 - Qiita](http://qiita.com/sl2/items/acfcca78cfa89ba73422)
- [JSONビュアーとしてVim(とjq)を使うとすごい便利 - Qiita](http://qiita.com/tekkoc/items/324d736f68b0f27680b8)
- [Pretty-print JSON in vim &mdash; Juan Uys](http://opyate.com/2013/11/05/my-new-favourite-way-to-pretty-print-json-in-vim.html)

これらの記事に書いてあるように，

```vim
:%!jq '.'
```

と外部コマンドを実行したり，

```vim
if executable('jq')
  function! s:jq(...)
    execute "%!jq " . (a:0 == 0 ? "'.'" : a:1)
  endfunction
  command! -bar -nargs=? Jq  call s:jq(<f-args>)
endif
```

のようなコマンドを定義し，```:Jq``` とコマンドを実行すればよい．

しかし，Json自体に誤りがあった場合は，Jsonを表示しているカレントバッファからJsonが消え，エラーメッセージに置き換えられてしまう．
いちいちundoするのも面倒だ．
そこで，エラーがある場合は，別のバッファにエラーメッセージを表示し，カレントバッファに変更を加えないようにしたい．
前回書いた記事：[VimでC言語のソースコードを整形したい](http://koturn.hatenablog.com/entry/2015/07/26/070619)を模倣し，コマンドを改良する．

```vim
if executable('jq')
  function! s:jq(has_bang, ...) abort range
    execute 'silent' a:firstline ',' a:lastline '!jq' string(a:0 == 0 ? '.' : a:1)
    if !v:shell_error || a:has_bang
      return
    endif
    let error_lines = filter(getline('1', '$'), 'v:val =~# "^parse error: "')
    " 範囲指定している場合のために，行番号を置き換える
    let error_lines = map(error_lines, 'substitute(v:val, "line \\zs\\(\\d\\+\\)\\ze,", "\\=(submatch(1) + a:firstline - 1)", "")')
    let winheight = len(error_lines) > 10 ? 10 : len(error_lines)
    " カレントバッファがエラーメッセージになっているので，元に戻す
    undo
    " カレントバッファの下に新たにウィンドウを作り，エラーメッセージを表示するバッファを作成する
    execute 'botright' winheight 'new'
    setl nobuflisted bufhidden=unload buftype=nofile
    call setline(1, error_lines)
    " エラーメッセージ用バッファのundo履歴を削除(エラーメッセージをundoで消去しないため)
    let save_undolevels = &l:undolevels
    setlocal undolevels=-1
    execute "normal! a \<BS>\<Esc>"
    setlocal nomodified
    let &l:undolevels = save_undolevels
    " エラーメッセージ用バッファは読み取り専用にしておく
    setlocal readonly
  endfunction
  command! -bar -bang -range=% -nargs=? Jq  <line1>,<line2>call s:jq(<bang>0, <f-args>)
endif
```

前回の記事とほとんど内容は変わらない．
フィルタリングに用いるコマンドのエラーを判別し，エラー用の処理を加えることで，いくつかの記事で紹介されているjqを用いるコマンドより便利になったというだけだ．


## 参考

- [jq コマンドを使う日常のご紹介 - Qiita](http://qiita.com/takeshinoda@github/items/2dec7a72930ec1f658af)
- [軽量JSONパーサー『jq』のドキュメント：『jq Manual』をざっくり日本語訳してみました ｜ Developers.IO](http://dev.classmethod.jp/tool/jq-manual-japanese-translation-roughly/)
- [jq コマンドが強力すぎてヤバい件 | CUBE SUGAR STORAGE](http://momijiame.tumblr.com/post/51299899637/jq)
- [jqコマンドが実は高性能すぎてビビッた話 - beatsync.net](https://beatsync.net/main/log20130428.html)
- [Vim上でjqでjsonを整形 - Qiita](http://qiita.com/sl2/items/acfcca78cfa89ba73422)
- [JSONビュアーとしてVim(とjq)を使うとすごい便利 - Qiita](http://qiita.com/tekkoc/items/324d736f68b0f27680b8)
- [Pretty-print JSON in vim &mdash; Juan Uys](http://opyate.com/2013/11/05/my-new-favourite-way-to-pretty-print-json-in-vim.html)
